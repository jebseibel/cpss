plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.7'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.liquibase.gradle' version '2.2.0'
}

group = 'com.seibel'
version = '0.0.2-SNAPSHOT'
description = 'cpss'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

ext {
    liquibaseVersion = '4.29.2'
    modelMapperVersion = '3.2.0'
    threetenbpVersion = '1.6.8'
    jakartaAnnotationVersion = '3.0.0'
    lombokVersion = '1.18.34'
    jjwtVersion = '0.12.6'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // --- Spring Boot Starters ---
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.6.0'

    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'

    // --- JWT ---
    implementation "io.jsonwebtoken:jjwt-api:${jjwtVersion}"
    runtimeOnly "io.jsonwebtoken:jjwt-impl:${jjwtVersion}"
    runtimeOnly "io.jsonwebtoken:jjwt-jackson:${jjwtVersion}"

    // --- Database ---
    implementation "org.liquibase:liquibase-core:${liquibaseVersion}"
    runtimeOnly 'com.mysql:mysql-connector-j'

    // --- Environment & Config ---
    implementation "me.paulschwarz:spring-dotenv:4.0.0"
    implementation 'org.yaml:snakeyaml:2.2'

    // --- Utilities ---
    implementation "org.modelmapper:modelmapper:${modelMapperVersion}"
    implementation "org.threeten:threetenbp:${threetenbpVersion}"
    implementation "jakarta.annotation:jakarta.annotation-api:${jakartaAnnotationVersion}"
    implementation 'org.apache.commons:commons-csv:1.11.0'

    // --- Development Tools ---
    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    // --- Lombok ---
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    // --- Testing ---
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    // --- Liquibase Runtime ---
    liquibaseRuntime 'org.liquibase:liquibase-core:4.29.2'
    liquibaseRuntime 'com.mysql:mysql-connector-j'
    liquibaseRuntime 'org.yaml:snakeyaml:2.2'
    liquibaseRuntime 'info.picocli:picocli:4.7.5'
}

tasks.named('test') {
    useJUnitPlatform()
}

// Frontend build tasks
task npmInstall(type: Exec) {
    group = 'frontend'
    description = 'Install frontend dependencies'
    workingDir 'frontend'
    commandLine 'npm', 'install'
}

task npmBuild(type: Exec) {
    group = 'frontend'
    description = 'Build frontend with Vite'
    dependsOn npmInstall
    workingDir 'frontend'
    commandLine 'npm', 'run', 'build'
}

task cleanStatic(type: Delete) {
    group = 'frontend'
    description = 'Clean static resources directory'
    delete 'src/main/resources/static'
}

task copyFrontend(type: Copy) {
    group = 'frontend'
    description = 'Copy frontend build to Spring Boot static resources'
    dependsOn npmBuild, cleanStatic
    from 'frontend/dist'
    into 'src/main/resources/static'
}

task killFrontend(type: Exec) {
    group = 'frontend'
    description = 'Kill frontend dev server (ports 5173-5175)'
    commandLine 'bash', '-c', 'lsof -ti:5173,5174,5175 2>/dev/null | xargs kill -9 2>/dev/null || echo "No frontend dev servers running"'
}

// Create deployment build task that includes frontend
task buildDeployment {
    group = 'build'
    description = 'Build JAR with frontend included for deployment'
    dependsOn copyFrontend, build

    // Ensure frontend is copied before processing resources
    tasks.processResources.mustRunAfter copyFrontend
}

// Liquibase configuration
liquibase {
    activities {
        main {
            changelogFile 'src/main/resources/db/changelog/db.changelog-master.yaml'
            url System.getenv('RDS_HOSTNAME') ?
                "jdbc:mysql://${System.getenv('RDS_HOSTNAME')}:${System.getenv('RDS_PORT')}/${System.getenv('RDS_DB_NAME')}" :
                'jdbc:mysql://localhost:3306/cpss'
            username System.getenv('RDS_USERNAME') ?: 'root'
            // Only set password if it exists to avoid the parameter issue
            if (System.getenv('RDS_PASSWORD')) {
                password System.getenv('RDS_PASSWORD')
            }
            driver 'com.mysql.cj.jdbc.Driver'
        }
    }
    runList = 'main'
}
